{
 "cells": [
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "reset()"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "load('../common_lib.sage')"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "## preparser will change / to * backslashOperator()\n",
    "## if the code is loaded, then it is fine\n",
    "preparser(False)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "%%writefile ../qbank/CountIntSol.sage\n",
    "\n",
    "def CountIntSol(param=(5,10), seed=None):\n",
    "    \"\"\"\n",
    "    A quiz generator for counting the number of integer solutions\n",
    "    \n",
    "    INPUT:\n",
    "    \n",
    "    - ``param`` -- tuple `(num_var, bound)`;\n",
    "      Consider the equation x1 + ... + xk = w;\n",
    "      3 <= k <= num_var;\n",
    "      3 <= w <= bound;\n",
    "      \n",
    "    - ``seed`` -- integer;  you may give a seed for  \n",
    "      the random process if  you like.  Otherwise  \n",
    "      the default is ``None`` and a random number  \n",
    "      will be chosen.\n",
    "      \n",
    "    OUTPUT:\n",
    "    \n",
    "    three strings: QUESTION, SOLUTION, CHECKCODE\n",
    "    \"\"\"\n",
    "    num_var,bound = param\n",
    "    random.seed(seed)\n",
    "    \n",
    "    ans = 0\n",
    "    while ans <= 10 or ans >= 10000: ### if the ans is too bad\n",
    "        k = random.randint(3, num_var)\n",
    "        w = random.randint(3, bound)\n",
    "        geq = [random.choice([0,1]) for _ in range(k)]\n",
    "        neq = random.choice(['=', r'\\leq'])\n",
    "\n",
    "        eqn = r\"x_1 + \\cdots + x_{%s} %s %s\"%(k,neq,w)\n",
    "        conds = ', '.join([r'x_%s\\geq %s'%(i+1,geq[i]) for i in range(k)])\n",
    "        \n",
    "        up = k if neq == '=' else k+1\n",
    "        down = w - sum(geq)\n",
    "        \n",
    "        ans = binomial(up+down-1, down)\n",
    "    \n",
    "    QUESTION = r\"\"\"Consider the equation \n",
    "\\[%(eqn)s\\]\n",
    "under the conditions\n",
    "\\[%(conds)s.\\]\n",
    "Count the number of the integer solutions.\n",
    "\n",
    "\\bigskip\n",
    "Check code $=$ (sum of all digits of your answer) mod $10$\n",
    "    \"\"\"%{'eqn': eqn, \n",
    "         'conds': conds, \n",
    "        }\n",
    "    \n",
    "    y_eqn = r\"y_1 + \\cdots + y_{%s} = %s\"%(up,down)\n",
    "    trans = ', '.join(['y_{%s} = x_%s %s'%(i+1,i+1,'-1' if geq[i] == 1 else '') \n",
    "                       for i in range(k)])\n",
    "    \n",
    "    checkcode = sum(int(d) for d in str(ans)) % 10\n",
    "    CHECKCODE = \"%s\"%checkcode\n",
    "    \n",
    "    SOLUTION = r\"\"\"The equivalent equation is \n",
    "\\[%(y_eqn)s,\\]\n",
    "where \n",
    "\\[%(trans)s.\\]\n",
    "    \n",
    "Therefore, the answer is \n",
    "\\[\\binom{%(up)s + %(down)s - 1}{%(down)s} = %(ans)s.\\]\n",
    "\n",
    "\\bigskip \n",
    "Check code $=$ (sum of all digits of your answer) mod $10$ $= %(checkcode)s$.\n",
    "    \"\"\"%({'y_eqn': y_eqn, \n",
    "          'trans': trans,\n",
    "          'up': up,\n",
    "          'down': down, \n",
    "          'ans': ans, \n",
    "          'checkcode': checkcode\n",
    "         })\n",
    "    \n",
    "    return QUESTION, SOLUTION, CHECKCODE"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "%%writefile ../qbank/Prufer.sage\n",
    "\n",
    "def Prufer(param=(8,), seed=None):\n",
    "    \"\"\"\n",
    "    A quiz generator for finding the Prufer code of a tree\n",
    "    \n",
    "    INPUT:\n",
    "    \n",
    "    - ``param`` -- tuple `(tree_order)`; \n",
    "      \n",
    "    - ``seed`` -- integer;  you may give a seed for  \n",
    "      the random process if  you like.  Otherwise  \n",
    "      the default is ``None`` and a random number  \n",
    "      will be chosen.\n",
    "      \n",
    "    OUTPUT:\n",
    "    \n",
    "    three strings: QUESTION, SOLUTION, CHECKCODE\n",
    "    \"\"\"\n",
    "    tree_order = param[0]\n",
    "    random.seed(seed)\n",
    "\n",
    "    g = graphs.RandomTree(tree_order)\n",
    "    ans = Prufer_code(g)\n",
    "    code = latex_graph_circular(g)\n",
    "\n",
    "    QUESTION = r\"\"\"Let $T$ be the labeled tree as shown below.\n",
    "\n",
    "\\begin{center}\n",
    "\\begin{tikzpicture}\n",
    "%(code)s\n",
    "\\end{tikzpicture}\n",
    "\\end{center}\n",
    "\n",
    "Find the Pr\\\"ufer code of $T$.\n",
    "\n",
    "\\bigskip\n",
    "Check code $=$ (sum of all digits of your answer) mod $10$\n",
    "    \"\"\"%{'code': code \n",
    "        }\n",
    "    \n",
    "    compact_ans = ''.join(str(d) for d in ans)\n",
    "    checkcode = sum(ans) % 10\n",
    "    CHECKCODE = \"%s\"%checkcode\n",
    "    \n",
    "    SOLUTION = r\"\"\"The Pr\\\"ufer code is $%(compact_ans)s$.\n",
    "\n",
    "\\bigskip \n",
    "Check code $=$ (sum of all digits of your answer) mod $10$ $= %(checkcode)s$.\n",
    "    \"\"\"%({'compact_ans': compact_ans, \n",
    "          'checkcode': checkcode\n",
    "         })\n",
    "    \n",
    "    return QUESTION, SOLUTION, CHECKCODE"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "%%writefile ../qbank/RecRel.sage\n",
    "\n",
    "def RecRel(param=(5,10), seed=None):\n",
    "    \"\"\"\n",
    "    A quiz generator for solving a recurrence relation\n",
    "    \n",
    "    INPUT:\n",
    "    \n",
    "    - ``param`` -- tuple `(r_bound, c_bound)`;\n",
    "      the general solution will be \n",
    "          c_1 r_1^n  + c_2 r_2^n\n",
    "      |r_i| <= r_bound\n",
    "      |c_i| <= c_bound\n",
    "      \n",
    "    - ``seed`` -- integer;  you may give a seed for  \n",
    "      the random process if  you like.  Otherwise  \n",
    "      the default is ``None`` and a random number  \n",
    "      will be chosen.\n",
    "      \n",
    "    OUTPUT:\n",
    "    \n",
    "    three strings: QUESTION, SOLUTION, CHECKCODE\n",
    "    \"\"\"\n",
    "    r_bound, c_bound = param\n",
    "    random.seed(seed)\n",
    "    \n",
    "    c_choices = list(range(-c_bound,c_bound+1))\n",
    "    c_choices.remove(0)\n",
    "    c1 = random.choice(c_choices)\n",
    "    c2 = random.choice(c_choices)\n",
    "\n",
    "    r_choices = list(range(-r_bound,r_bound+1))\n",
    "    r_choices.remove(0)\n",
    "    r_choices.remove(1)\n",
    "    r1 = random.choice(r_choices)\n",
    "    r2 = random.choice(r_choices)\n",
    "    \n",
    "    g_sol = r\"(%s)\\cdot(%s)^n + (%s)\\cdot(%s)^n\"%(c1,r1,c2,r2)\n",
    "    ak = lambda k: c1 * (r1)**k + c2 * (r2)**k\n",
    "    a0 = ak(0)\n",
    "    a1 = ak(1)\n",
    "    \n",
    "    p,q = r1+r2, -r1*r2\n",
    "    rel = r\"a_n = (%s) a_{n-1} + (%s) a_{n-2}\"%(p,q)\n",
    "    char_eq = \"x^2 = (%s)x + (%s)\"%(p,q)\n",
    "\n",
    "    QUESTION = r\"\"\"Solve the recurrence relation\n",
    "\\[%(rel)s \\text{ for }n\\geq 2,\\]\n",
    "\\[a_0 = %(a0)s, a_1 = %(a1)s.\\]\n",
    "Write your solution in the form of \n",
    "\\[a_n = c_1\\cdot r_1^n + c_2\\cdot r_2^n\\]\n",
    "\n",
    "\\bigskip\n",
    "Check code $=$ $(c_1+c_2+r_1+r_2)$ mod $10$\n",
    "    \"\"\"%{'rel': rel,\n",
    "         'a0': a0,\n",
    "         'a1': a1\n",
    "        }\n",
    "    \n",
    "    checkcode = (c1 + c2 + r1 + r2) % 10\n",
    "    CHECKCODE = \"%s\"%checkcode\n",
    "    \n",
    "    SOLUTION = r\"\"\"Solve the characteristic polynomial \n",
    "\\[%(char_eq)s\\]\n",
    "and get \n",
    "\\[r_1 = %(r1)s, r_2 = %(r2)s.\\]\n",
    "\n",
    "Then solve the system of linear equations\n",
    "\\[a_0 = c_1 + c_2 = %(a0)s\\]\n",
    "\\[a_1 = (%(r1)s) c_1 + (%(r2)s) c_2 = %(a1)s\\]\n",
    "to get \n",
    "\\[c_1 = %(c1)s, c_2 = %(c2)s.\\]\n",
    "\n",
    "\\bigskip \n",
    "Check code $=$ $(c_1+c_2+r_1+r_2)$ mod $10$ $= %(checkcode)s$.\n",
    "    \"\"\"%({'char_eq': char_eq,\n",
    "          'r1': r1,\n",
    "          'r2': r2, \n",
    "          'c1': c1, \n",
    "          'c2': c2, \n",
    "          'a0': a0,\n",
    "          'a1': a1, \n",
    "          'checkcode': checkcode\n",
    "         })\n",
    "    \n",
    "    return QUESTION, SOLUTION, CHECKCODE"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "q, s, c = RecRel()"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "print(q)\n",
    "print('---')\n",
    "print(s)\n",
    "print('---')\n",
    "print(c)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": []
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": []
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "SageMath 9.1",
   "language": "sage",
   "name": "sagemath"
  },
  "language_info": {
   "codemirror_mode": {
    "name": "ipython",
    "version": 3
   },
   "file_extension": ".py",
   "mimetype": "text/x-python",
   "name": "python",
   "nbconvert_exporter": "python",
   "pygments_lexer": "ipython3",
   "version": "3.8.5"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 2
}
